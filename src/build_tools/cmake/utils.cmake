INCLUDE(${OpenRDK_CMAKE_MODULE_PATH}/prettymessage.cmake)

# Macro to add an OpenRDK module in the build system; it links against rdkcore
MACRO(RDK_ADD_RAGENT_MODULE PARAM_FILE_LIST)
	STRING(REGEX REPLACE ".*/([^/]*)$" "\\1" MODULE_NAME ${CMAKE_CURRENT_SOURCE_DIR})
	SET(FILE_LIST ${PARAM_FILE_LIST})
	IF("${FILE_LIST}" STREQUAL "ALL_FILES" OR "${FILE_LIST}" STREQUAL "AUTO")
		FILE(GLOB FILE_LIST "*.cpp" "*.c")
	ENDIF("${FILE_LIST}" STREQUAL "ALL_FILES" OR "${FILE_LIST}" STREQUAL "AUTO")
	IF("${FILE_LIST}" STREQUAL "ALL_FILES_RECURSE")
		FILE(GLOB_RECURSE FILE_LIST "*.cpp" "*.c")
	ENDIF("${FILE_LIST}" STREQUAL "ALL_FILES_RECURSE")
	SET(THIS_MODULE_NAME "rdkram_${MODULE_NAME}")
	SET(RDK_THIS_MODULE_NAME ${THIS_MODULE_NAME})
	IF(RDK_INSIDE_OpenRDK)
		SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/lib")
	ELSE(RDK_INSIDE_OpenRDK)
		SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/libext")
	ENDIF(RDK_INSIDE_OpenRDK)

	GET_FILENAME_COMPONENT(THIS_TARGET_DIR ${CMAKE_CURRENT_SOURCE_DIR} NAME)
	LIST(FIND RDK_FORCE_REMOVE_TARGET_LIST ${THIS_TARGET_DIR} TO_REMOVE)
	IF(NOT ${TO_REMOVE} EQUAL -1)
		RDK_ADD_FORCE_REMOVE_COMMAND(${RDK_THIS_MODULE_NAME})
	ELSE(NOT ${TO_REMOVE} EQUAL -1)
		ADD_LIBRARY(${THIS_MODULE_NAME} SHARED ${FILE_LIST})
		TARGET_LINK_LIBRARIES(${THIS_MODULE_NAME} ${RDKCORE_LIBRARIES})
		SET_TARGET_PROPERTIES(${THIS_MODULE_NAME} PROPERTIES COMPILE_FLAGS "${RDKCORE_CPPFLAGS} ${RDKCORE_CXXFLAGS}")
		SET_TARGET_PROPERTIES(${THIS_MODULE_NAME} PROPERTIES LINK_FLAGS "${RDKCORE_LDFLAGS}")
		# NOTE: depending on the operating system, shared libraries and their static counterpart are treated as LIBRARY, RUNTIME or ARCHIVE
		SET_TARGET_PROPERTIES(${THIS_MODULE_NAME} PROPERTIES LIBRARY_OUTPUT_DIRECTORY "${OUT_DIR}")
		SET_TARGET_PROPERTIES(${THIS_MODULE_NAME} PROPERTIES RUNTIME_OUTPUT_DIRECTORY "${OUT_DIR}")
		SET_TARGET_PROPERTIES(${THIS_MODULE_NAME} PROPERTIES ARCHIVE_OUTPUT_DIRECTORY "${OUT_DIR}")
		verbose("Adding module \"${RDK_THIS_MODULE_NAME}\"")
	ENDIF(NOT ${TO_REMOVE} EQUAL -1)
ENDMACRO(RDK_ADD_RAGENT_MODULE)

# Macro to add an application that links to rdkcore
MACRO(RDK_ADD_EXECUTABLE PARAM_EXE_NAME PARAM_FILE_LIST)
	SET(EXE_NAME "${PARAM_EXE_NAME}")
	IF("${EXE_NAME}" MATCHES "AUTO")
		STRING(REGEX REPLACE ".*/([^/]*)$" "\\1" EXE_NAME ${CMAKE_CURRENT_SOURCE_DIR})
	ENDIF("${EXE_NAME}" MATCHES "AUTO")
	SET(FILE_LIST ${PARAM_FILE_LIST})
	IF("${FILE_LIST}" MATCHES "ALL_FILES")
		FILE(GLOB FILE_LIST "*.cpp")
	ENDIF("${FILE_LIST}" MATCHES "ALL_FILES")
	SET(RDK_THIS_EXE_NAME ${EXE_NAME})
	IF(RDK_INSIDE_OpenRDK)
		SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/bin")
	ELSE(RDK_INSIDE_OpenRDK)
		SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/binext")
	ENDIF(RDK_INSIDE_OpenRDK)

	GET_FILENAME_COMPONENT(THIS_TARGET_DIR ${CMAKE_CURRENT_SOURCE_DIR} NAME)
	LIST(FIND RDK_FORCE_REMOVE_TARGET_LIST ${THIS_TARGET_DIR} TO_REMOVE)
	IF(NOT ${TO_REMOVE} EQUAL -1)
		RDK_ADD_FORCE_REMOVE_COMMAND(${RDK_THIS_EXE_NAME})
	ELSE(NOT ${TO_REMOVE} EQUAL -1)
		ADD_EXECUTABLE(${EXE_NAME} ${FILE_LIST})
		TARGET_LINK_LIBRARIES(${EXE_NAME} ${RDKCORE_LIBRARIES})
		SET_TARGET_PROPERTIES(${RDK_THIS_EXE_NAME} PROPERTIES COMPILE_FLAGS "${RDKCORE_CPPFLAGS} ${RDKCORE_CXXFLAGS}")
		SET_TARGET_PROPERTIES(${RDK_THIS_EXE_NAME} PROPERTIES LINK_FLAGS "${RDKCORE_LDFLAGS}")
		# NOTE: depending on the operating system, shared libraries and their static counterpart are treated as LIBRARY, RUNTIME or ARCHIVE
		SET_TARGET_PROPERTIES(${RDK_THIS_EXE_NAME} PROPERTIES LIBRARY_OUTPUT_DIRECTORY "${OUT_DIR}")
		SET_TARGET_PROPERTIES(${RDK_THIS_EXE_NAME} PROPERTIES RUNTIME_OUTPUT_DIRECTORY "${OUT_DIR}")
		SET_TARGET_PROPERTIES(${RDK_THIS_EXE_NAME} PROPERTIES ARCHIVE_OUTPUT_DIRECTORY "${OUT_DIR}")
	verbose("Adding executable \"${RDK_THIS_EXE_NAME}\"")
	ENDIF(NOT ${TO_REMOVE} EQUAL -1)
ENDMACRO(RDK_ADD_EXECUTABLE)

# Macro to add an library in the build system; it may not link rdkcore
MACRO(RDK_ADD_LIBRARY)
	STRING(REGEX REPLACE ".*/([^/]*)$" "\\1" LIBRARY_NAME ${CMAKE_CURRENT_SOURCE_DIR})
	SET(FILE_LIST ${ARGV0})
	IF("${FILE_LIST}" STREQUAL "ALL_FILES")
		FILE(GLOB FILE_LIST "*.cpp")
	ELSEIF("${FILE_LIST}" STREQUAL "ALL_FILES_RECURSE")
		FILE(GLOB_RECURSE FILE_LIST "*.cpp")
	ENDIF("${FILE_LIST}" STREQUAL "ALL_FILES")
	IF(NOT "${ARGV1}" STREQUAL "")
		SET(LIBRARY_NAME "${ARGV1}")
	ENDIF(NOT "${ARGV1}" STREQUAL "")
	SET(THIS_LIBRARY_NAME "rdk_${LIBRARY_NAME}")
	SET(RDK_THIS_LIBRARY_NAME ${THIS_LIBRARY_NAME})

	GET_FILENAME_COMPONENT(THIS_TARGET_DIR ${CMAKE_CURRENT_SOURCE_DIR} NAME)
	LIST(FIND RDK_FORCE_REMOVE_TARGET_LIST ${THIS_TARGET_DIR} TO_REMOVE)
	IF(NOT ${TO_REMOVE} EQUAL -1)
		RDK_ADD_FORCE_REMOVE_COMMAND(${RDK_THIS_LIBRARY_NAME})
	ELSE(NOT ${TO_REMOVE} EQUAL -1)
		IF("${FILE_LIST}" STREQUAL "")
			verbose("WARNING: Empty library: no source files specified for library \"${THIS_LIBRARY_NAME}\"")
		ELSE("${FILE_LIST}" STREQUAL "")
			verbose("Adding library \"${RDK_THIS_LIBRARY_NAME}\"")
			ADD_LIBRARY(${THIS_LIBRARY_NAME} SHARED ${FILE_LIST})
			IF(RDK_INSIDE_OpenRDK)
				SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/lib")
			ELSE(RDK_INSIDE_OpenRDK)
				SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/libext")
			ENDIF(RDK_INSIDE_OpenRDK)
			TARGET_LINK_LIBRARIES(${THIS_LIBRARY_NAME} ${RDKCORE_LIBRARIES})
			SET_TARGET_PROPERTIES(${THIS_LIBRARY_NAME} PROPERTIES COMPILE_FLAGS "${RDKCORE_CPPFLAGS} ${RDKCORE_CXXFLAGS}")
			SET_TARGET_PROPERTIES(${THIS_LIBRARY_NAME} PROPERTIES LINK_FLAGS "${RDKCORE_LDFLAGS}")
			# NOTE: depending on the operating system, shared libraries and their static counterpart are treated as LIBRARY, RUNTIME or ARCHIVE
			SET_TARGET_PROPERTIES(${THIS_LIBRARY_NAME} PROPERTIES LIBRARY_OUTPUT_DIRECTORY "${OUT_DIR}")
			SET_TARGET_PROPERTIES(${THIS_LIBRARY_NAME} PROPERTIES RUNTIME_OUTPUT_DIRECTORY "${OUT_DIR}")
			SET_TARGET_PROPERTIES(${THIS_LIBRARY_NAME} PROPERTIES ARCHIVE_OUTPUT_DIRECTORY "${OUT_DIR}")
		ENDIF("${FILE_LIST}" STREQUAL "")
	ENDIF(NOT ${TO_REMOVE} EQUAL -1)
ENDMACRO(RDK_ADD_LIBRARY)

# Macro to recursively add all subdirectories to the build tree
MACRO(RDK_ADD_ALL_SUBDIRECTORIES)

	IF(${ARGC} EQUAL 0)
		EXEC_PROGRAM("find . -maxdepth 1 -type d -not -iname .svn -not -iname . -not -iname cmakefiles -not -iname oldies" ${CMAKE_CURRENT_SOURCE_DIR} OUTPUT_VARIABLE tempdirs)
	ELSE(${ARGC} EQUAL 0)
		STRING(REPLACE ";" "\n" tempdirs "${ARGV}")
	ENDIF(${ARGC} EQUAL 0)

	IF(NOT ${tempdirs} STREQUAL "")
		STRING(REPLACE "\n" ";" dirs ${tempdirs})
		FOREACH(d ${dirs})
			IF(NOT "${d}" MATCHES "(CMake.*|dontcompile_*|oldies)")
				EXEC_PROGRAM("find ${d} -maxdepth 1 -type f -name DO_NOT_COMPILE" ${CMAKE_CURRENT_SOURCE_DIR} OUTPUT_VARIABLE excludedir)
				IF(NOT "${excludedir}" STREQUAL "")
					GET_FILENAME_COMPONENT(THIS_TARGET_DIR_tmp ${excludedir} PATH)
					GET_FILENAME_COMPONENT(THIS_TARGET_DIR ${THIS_TARGET_DIR_tmp} NAME)
					LIST(APPEND RDK_FORCE_REMOVE_TARGET_LIST ${THIS_TARGET_DIR})
				ENDIF(NOT "${excludedir}" STREQUAL "")
				GET_FILENAME_COMPONENT(cmakeliststxt "${d}/CMakeLists.txt" ABSOLUTE)
				IF(EXISTS "${cmakeliststxt}")
					ADD_SUBDIRECTORY(${d})
				ENDIF(EXISTS "${cmakeliststxt}")
			ENDIF(NOT "${d}" MATCHES "(CMake.*|dontcompile_*|oldies)")
		ENDFOREACH(d ${dirs})
	ENDIF(NOT ${tempdirs} STREQUAL "")
ENDMACRO(RDK_ADD_ALL_SUBDIRECTORIES)

# Macro for building rconsoleqt modules
MACRO(RCONSOLE_QT_MODULE)
	STRING(REGEX REPLACE ".*/([^/]*)$" "\\1" MODULE_NAME ${CMAKE_CURRENT_SOURCE_DIR})
	IF("${COMPILE_FILES}" MATCHES "AUTO")
		FILE(GLOB COMPILE_FILE_LIST "*.cpp")
	ELSE("${COMPILE_FILES}" MATCHES "AUTO")
		SET(COMPILE_FILE_LIST ${COMPILE_FILES})
	ENDIF("${COMPILE_FILES}" MATCHES "AUTO")
	#MESSAGE(STATUS "RConsoleQt module: ${MODULE_NAME}")
	#MESSAGE(STATUS "Will compile these:" "${COMPILE_FILE_LIST}")
	#MESSAGE(STATUS "Will moc these: " "${MOC_FILES}")

	SET(THIS_MODULE_NAME "rdkrqm_${MODULE_NAME}")
	SET(RDK_THIS_RQ_MODULE_NAME ${THIS_MODULE_NAME})
	IF(RDK_INSIDE_OpenRDK)
		SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/lib")
	ELSE(RDK_INSIDE_OpenRDK)
		SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/libext")
	ENDIF(RDK_INSIDE_OpenRDK)

	SET(MOCD_FILES "")
	FOREACH(F ${MOC_FILES})
		QT_WRAP_CPP(${RDK_THIS_RQ_MODULE_NAME} MOCD_FILES "${F}")
	ENDFOREACH(F ${MOC_FILES})
	#MESSAGE(STATUS "These are moc'd:" "${MOCD_FILES}")
	INCLUDE_DIRECTORIES(${QT_INCLUDE_DIR})
	INCLUDE_DIRECTORIES(${RDKCORE_INCLUDE_DIR})
	LINK_DIRECTORIES(${RDKCORE_LINK_DIRECTORIES})
	ADD_DEFINITIONS(${RDKCORE_DEFINITIONS})
	ADD_LIBRARY(${RDK_THIS_RQ_MODULE_NAME} SHARED ${COMPILE_FILE_LIST} ${MOCD_FILES})
	# not available yet, thus skipping this for now
	#IF(NOT ${CMAKE_SYSTEM} MATCHES "CYGWIN")
	#  INSTALL(TARGETS rdkrqm_${MODULE_NAME} LIBRARY DESTINATION ${LIBRARY_INSTALL_PATH})
	#ELSE(NOT ${CMAKE_SYSTEM} MATCHES "CYGWIN")
	#  INSTALL(TARGETS rdkrqm_${MODULE_NAME} RUNTIME DESTINATION ${LIBRARY_INSTALL_PATH})
	#ENDIF(NOT ${CMAKE_SYSTEM} MATCHES "CYGWIN")
	TARGET_LINK_LIBRARIES(${RDK_THIS_RQ_MODULE_NAME} rdkcore)
	TARGET_LINK_LIBRARIES(${RDK_THIS_RQ_MODULE_NAME} ${QT_LIBRARIES} ${QT_QT_LIBRARY})
	TARGET_LINK_LIBRARIES(${RDK_THIS_RQ_MODULE_NAME} rdkrq_common)
	TARGET_LINK_LIBRARIES(${RDK_THIS_RQ_MODULE_NAME} ${LINK_LIBS})

	SET_TARGET_PROPERTIES(${RDK_THIS_RQ_MODULE_NAME} PROPERTIES LIBRARY_OUTPUT_DIRECTORY "${OUT_DIR}")
	SET_TARGET_PROPERTIES(${RDK_THIS_RQ_MODULE_NAME} PROPERTIES RUNTIME_OUTPUT_DIRECTORY "${OUT_DIR}")
	SET_TARGET_PROPERTIES(${RDK_THIS_RQ_MODULE_NAME} PROPERTIES ARCHIVE_OUTPUT_DIRECTORY "${OUT_DIR}")
	verbose("Adding rq module \"${RDK_THIS_RQ_MODULE_NAME}\"")

ENDMACRO(RCONSOLE_QT_MODULE)


# Macro for force-removing old target (DO_NOT_COMPILE stuff)
MACRO(RDK_ADD_FORCE_REMOVE_COMMAND RDK_SONAME)
	IF(RDK_INSIDE_OpenRDK)
		SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/lib")
	ELSE(RDK_INSIDE_OpenRDK)
		SET(OUT_DIR "${OpenRDK_OUTPUT_TREE}/libext")
	ENDIF(RDK_INSIDE_OpenRDK)
	verbose("Removing old target [${RDK_SONAME}]")
	ADD_CUSTOM_TARGET(${RDK_SONAME} ALL rm -f *${RDK_SONAME}* WORKING_DIRECTORY ${OUT_DIR} COMMENT "Force-removing old target ${RDK_SONAME}, if it exists")
ENDMACRO(RDK_ADD_FORCE_REMOVE_COMMAND RDK_SONAME)

# Macro for printing some useful information
MACRO(RDK_PRINT_VARIABLES)
	info("")
	info("Found ${OpenRDKLOCAL}OpenRDK Installation (version: ${OpenRDK_VERSION} arch: ${OpenRDK_ARCH})")
	info("Found project ${PROJECT_NAME} (version: ${PROJECT_VERSION} arch: ${PROJECT_ARCH})")
	info("")
	info("Variables:")
	info("PROJECT_ROOT = ${PROJECT_ROOT}")
	verbose("PROJECT_LIBRARY_OUTPUT_PATH = ${PROJECT_LIBRARY_OUTPUT_PATH}")
	info("OpenRDK_ROOT = ${OpenRDK_ROOT}")
	info("OpenRDK_OUTPUT_TREE = ${OpenRDK_OUTPUT_TREE}")
	verbose("RDKCORE_INCLUDE_DIR = ${RDKCORE_INCLUDE_DIR}")
	verbose("RDKCORE_DEFINITIONS = ${RDKCORE_DEFINITIONS}")
	verbose("RDKCORE_LINK_DIRECTORIES = ${RDKCORE_LINK_DIRECTORIES}")
	verbose("RDKCORE_LIBRARIES = ${RDKCORE_LIBRARIES}")
	verbose("RDKCORE_CXXFLAGS = ${RDKCORE_CXXFLAGS}")
	verbose("RDKCORE_LDFLAGS = ${RDKCORE_LDFLAGS}")

	IF("${CMAKE_CROSSCOMPILING}" STREQUAL TRUE)
		info("")
		info("..:: Cross Compiling Detected ::..")
		info("CMAKE_CXX_COMPILER = ${CMAKE_CXX_COMPILER}")
		info("CMAKE_C_COMPILER = ${CMAKE_C_COMPILER}")
		info("CMAKE_AR = ${CMAKE_AR}")
		info("CMAKE_RANLIB = ${CMAKE_RANLIB}")
		info("CMAKE_LD = ${CMAKE_LINKER}")
		info("CMAKE_STRIP = ${CMAKE_STRIP}")
		info("CMAKE_OBJCOPY = ${CMAKE_OBJCOPY}")
		info("CMAKE_OBJDUMP = ${CMAKE_OBJDUMP}")
	ENDIF("${CMAKE_CROSSCOMPILING}" STREQUAL TRUE)

ENDMACRO(RDK_PRINT_VARIABLES)
